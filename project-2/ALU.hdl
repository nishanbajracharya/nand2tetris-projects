/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // Zero x if zx=true
    Mux16(a=x, b=false, sel=zx, out=xFromZX);

    // Negate x if nx=true
    Not16(in=xFromZX, out=NotxFromZX);
    Mux16(a=xFromZX, b=NotxFromZX, sel=nx, out=xFromNX);

    // Zero y if zy=true
    Mux16(a=y, b=false, sel=zy, out=yFromZY);

    // Negate y if ny=true
    Not16(in=yFromZY, out=NotyFromZY);
    Mux16(a=yFromZY, b=NotyFromZY, sel=ny, out=yFromNY);

    // Calculate f(x,y)
    Add16(a=xFromNX, b=yFromNY, out=addXY);
    And16(a=xFromNX, b=yFromNY, out=andXY);
    Mux16(a=andXY, b=addXY, sel=f, out=fXY);

    // Negate out if no=true
    Not16(in=fXY, out=NotfXY);
    Mux16(a=fXY, b=NotfXY, sel=no, out=out, out[15]=msb, out[0..7]=low, out[8..15]=high);

    // Check if out = 0
    Or8Way(in=low, out=lowOr8);
    Or8Way(in=high, out=highOr8);
    Or(a=lowOr8, b=highOr8, out=Notzr);
    Not(in=Notzr, out=zr);

    // Check if out < 0
    And(a=msb, b=true, out=ng);
}